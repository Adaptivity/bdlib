buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "bdew"
            url = "http://jenkins.bdew.net/maven"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'scala'

version = "0.9.0"

if (project.hasProperty('buildnum')) {
    project.version = project.version + '.' + project.buildnum
} else {
    project.version = project.version + '-dev'
}

group = "net.bdew"
archivesBaseName = "bdlib"

minecraft {
    version = "1.6.4-9.11.1.964"
}

import org.apache.tools.ant.filters.ReplaceTokens

def myTokens = [BdLibVer: project.version]

sourceSets {
    main {
        scala {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
    processed {
        scala {
            srcDir 'build/stage'
        }
    }
}

task stage(type: Sync) {
    from sourceSets.main.scala
    into 'build/stage'
    filter(ReplaceTokens, tokens: myTokens)
}

compileScala {
    source = sourceSets.processed.scala
    dependsOn stage
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        filter(ReplaceTokens, tokens: myTokens)
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
}

jar {
    appendix = 'universal'
}

artifacts {
    archives sourceJar
    archives deobfJar
}

apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            artifact deobfJar

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "file://var/www/maven"
        }
    }
}